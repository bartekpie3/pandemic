plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.5'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'nu.studer.jooq' version '10.1.1'
}

group = 'com.example'
version = '1.0.0'
description = 'Pandemic board game'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(24)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    runtimeOnly 'org.postgresql:postgresql'

    // JOOQ
    implementation 'org.springframework.boot:spring-boot-starter-jooq'
    jooqGenerator "org.jooq:jooq-meta-extensions:3.19.25"
    jooqGenerator("org.postgresql:postgresql")

    // lombok
    compileOnly("org.projectlombok:lombok:1.18.38")
    annotationProcessor("org.projectlombok:lombok:1.18.38")
    testCompileOnly("org.projectlombok:lombok:1.18.38")
    testAnnotationProcessor("org.projectlombok:lombok:1.18.38")

    // tests
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}

// JOOQ

jooq {
    version = '3.19.25'
    configurations {
        main {
            generationTool {
                jdbc {
                    driver = 'org.postgresql.Driver'
                    url = 'jdbc:postgresql://localhost:5432/pandemic'
                    user = 'pandemic'
                    password = 'pandemic'
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        inputSchema = 'public'
                        properties {
                            property {
                                key = 'scripts'
                                value = 'src/main/resources/sql/init-schema.sql'
                            }
                        }
                    }
                    target {
                        packageName = 'com.example.jooq.generated'
                        directory = "$buildDir/generated-src/jooq/main"
                    }
                }
            }
        }
    }
}

sourceSets {
    main {
        java {
            srcDir "$buildDir/generated-src/jooq/main"
        }
    }
}